plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "com.google.devtools.ksp"
    id "com.google.dagger.hilt.android"
}

android {
    def appId = "tmidev.localaccount"
    def minApi = 21
    def targetApi = 33
    def appVersionCode = 1
    def appVersionName = "1.0.0"

    namespace appId
    compileSdk targetApi

    defaultConfig {
        applicationId appId
        minSdk minApi
        targetSdk targetApi
        versionCode appVersionCode
        versionName appVersionName
        vectorDrawables.useSupportLibrary true
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
        }

        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility(javaVer)
        targetCompatibility(javaVer)
    }

    kotlinOptions.jvmTarget = javaVer

    buildFeatures.compose true

    composeOptions.kotlinCompilerExtensionVersion composeCompilerVer

    packagingOptions.resources.excludes += "/META-INF/{AL2.0,LGPL2.1}"
}

dependencies {
    implementation "androidx.core:core-ktx:1.10.0"

    implementation "androidx.activity:activity-compose:1.7.1"

    implementation platform("androidx.compose:compose-bom:2023.04.01")
    implementation "androidx.compose.animation:animation"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.material:material-icons-extended"

    implementation "com.google.accompanist:accompanist-systemuicontroller:0.30.1"

    implementation "androidx.navigation:navigation-compose:2.5.3"

    implementation "com.google.dagger:hilt-android:$hiltVer"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVer"

    implementation "androidx.hilt:hilt-navigation-compose:$hiltExtVer"
    kapt "androidx.hilt:hilt-compiler:$hiltExtVer"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVer"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycleVer"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycleVer"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation "androidx.room:room-runtime:$roomVer"
    implementation "androidx.room:room-ktx:$roomVer"
    ksp "androidx.room:room-compiler:$roomVer"
}